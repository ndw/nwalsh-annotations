buildscript {
  repositories {
    mavenCentral()
  }
}

plugins {
  id "java"
  id "maven"
  id "signing"
}

repositories {
  mavenCentral()
}

dependencies {
  compile (
    [group: 'org.atteo.classindex', name: 'classindex', version: '3.3'],
  )
}

sourceSets {
  main {
    java {
      srcDir 'src/main/java'
    }
  }
}

jar {
  archiveName "nwalsh-annotations-" + version + ".jar"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task copyJar(dependsOn: jar, type: Copy) {
  from "build/libs/nwalsh-annotations-" + version + ".jar"
  into "build/dist"
}

task copyJavaDoc(dependsOn: javadoc, type: Copy) {
  from "build/docs"
  into "build/dist/docs"
}

task copyLicense(dependsOn: copyJavaDoc, type: Copy) {
  from "LICENSE.md"
  into "build/dist/docs"
}

task copyReadme(dependsOn: copyJavaDoc, type: Copy) {
  from "README.md"
  into "build/dist/docs"
}

task copyDocs(dependsOn: [copyLicense, copyReadme]) {
  // nop
}

task makeDist(dependsOn: [ build, copyJar, copyDocs ]) << {
  println "Created distribution in build/dist"
}

task zipDist(dependsOn: makeDist, type: Zip) {
  from('build/dist')
  into 'nwalsh-annotations-' + version
  archiveName 'nwalsh-annotations-' + version + ".zip"
}

task dist(dependsOn: zipDist) {
  // nop
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

signing {
  required { gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
               name 'annotations'
               packaging 'jar'
               description 'A set of global, public annotations'
               url 'https://github.com/ndw/nwalsh-annotations'

               scm {
                   url 'scm:git@github.com:ndw/nwalsh-annotations.git'
                   connection 'scm:git@github.com:ndw/nwalsh-annotations.git'
                   developerConnection 'scm:git@github.com:ndw/nwalsh-annotations.git'
               }

               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'ndw'
                       name 'Norman Walsh'
                   }
               }
           }
        }
    }
}
